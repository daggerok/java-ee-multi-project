plugins {
  id "ear"
  id "com.avast.gradle.docker-compose" version "0.6.13"
}

repositories { mavenCentral() }

dependencies {
  deploy project(":ejb-data")
  deploy project(":ejb-services")
  [":servlet", ":jsp", ":rest", ":web"].each {
    deploy project(path: it, configuration: "archives")
  }
}

ear {
  deploymentDescriptor {
    version = "7"
    description = "$rootProject.name Gradle EAR"
    [":servlet", ":jsp", ":rest", ":web"].each {
      def p = project(it)
      webModule("${p.name}-${p.version}.war", "/$p.name")
    }
  }
}

def profile = project.hasProperty("profile") ? project.getProperty("profile") : "gradle"

dockerCompose {
  useComposeFiles = [project.file("docker-compose-${profile}.yaml")]
  // captureContainersOutput = true
  captureContainersOutput = false
  stopContainers = true
  removeContainers = true
  removeImages = "Local"
  removeVolumes = true
  removeOrphans = true
  forceRecreate = true
  waitForTcpPorts = false
  projectName = project.name
}

composeUp.dependsOn assemble
